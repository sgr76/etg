# Find the native LLVM includes and libraries
#
# Defines the following variables
#  LLVM_INCLUDE_DIRS
#  LLVM_LIBRARY_DIRS
#  LLVM_CFLAGS         
#  LLVM_LFLAGS         
#  LLVM_MODULE_LIBS
#  LLVM_INSTALL_PREFIX
#  LLVM_FOUND          
#  LLVM_VERSION       

find_program (LLVM_CONFIG_EXECUTABLE NAMES llvm-config DOC "llvm-config executable")

set (LLVM_FOUND FALSE)

if (LLVM_CONFIG_EXECUTABLE)
    execute_process(COMMAND ${LLVM_CONFIG_EXECUTABLE} --version OUTPUT_VARIABLE LLVM_VERSION OUTPUT_STRIP_TRAILING_WHITESPACE)

    if (LLVM_VERSION)
        set (LLVM_FOUND TRUE)
    endif()
else()
    if (LLVM_FIND_REQUIRED)
        message (FATAL_ERROR "Could NOT find 'llvm-config' executable")
    else ()
        message ("Could NOT find 'llvm-config' executable")
    endif ()
endif()

if (LLVM_FOUND)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --includedir OUTPUT_VARIABLE LLVM_INCLUDE_DIRS OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --libdir OUTPUT_VARIABLE LLVM_LIBRARY_DIRS OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --cppflags OUTPUT_VARIABLE LLVM_CFLAGS OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --ldflags OUTPUT_VARIABLE LLVM_LFLAGS OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --libs core bitreader asmparser analysis OUTPUT_VARIABLE LLVM_MODULE_LIBS OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --libfiles OUTPUT_VARIABLE LLVM_LIBS OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process (COMMAND ${LLVM_CONFIG_EXECUTABLE} --prefix OUTPUT_VARIABLE LLVM_INSTALL_PREFIX OUTPUT_STRIP_TRAILING_WHITESPACE)
endif()
